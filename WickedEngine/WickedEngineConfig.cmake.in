cmake_minimum_required(VERSION 3.8)

get_filename_component(WICKED_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)
include(CMakeFindDependencyMacro)


if (WIN32)
    # TODO: Choose whether to use SDL3 on windows as well
    set(TARGET_NAME WickedEngine::WickedEngine_Windows)
    set(LIBDXCOMPILER "dxcompiler.dll")
else ()
    set(TARGET_NAME WickedEngine::WickedEngine_Linux)
    find_package(Vulkan REQUIRED)
    find_package(SDL3 REQUIRED)
    find_package(OpenImageDenoise QUIET)
    find_package(Threads REQUIRED)
    if(NOT ${OpenImageDenoise_FOUND})
        message("OpenImageDenoise not found, it will be disabled.")
    else()
        message("OpenImageDenoise Found.")
    endif()
    set(LIBDXCOMPILER "libdxcompiler.so")
endif()

if (NOT TARGET WickedEngine::WickedEngine)
    include("${WICKED_CMAKE_DIR}/WickedEngineTargets.cmake")
    add_library(WickedEngine::WickedEngine ALIAS ${TARGET_NAME})

    set(WickedEngine_SHADER_DIR "@_CONFIG_INSTALL_DIR_INCLUDE@/WickedEngine/shaders/")
    target_compile_definitions(${TARGET_NAME} INTERFACE "WickedEngine_SHADER_DIR=\"${WickedEngine_SHADER_DIR}\"")

    add_library(WickedEngine::dxcompiler SHARED IMPORTED)
    set_property(TARGET WickedEngine::dxcompiler PROPERTY
            IMPORTED_LOCATION "@_CONFIG_INSTALL_DIR_LIB@/WickedEngine/${LIBDXCOMPILER}" )
endif()

set(WickedEngine_LIBRARIES WickedEngine::WickedEngine ${TARGET_NAME})
